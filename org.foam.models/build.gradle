import org.eclipse.emf.mwe.utils.DirectoryCleaner
import org.eclipse.emf.mwe.utils.StandaloneSetup
import org.eclipse.emf.mwe2.ecore.EcoreGenerator


ext.EMF_GEN_DIR	= "main/emf-gen"
ext.MODELSDIR	= "models"
ext.MODELS	= "platform:/resource/org.foam.models/" + MODELSDIR

defaultTasks 'generateModels'

buildscript {
	dependencies {
		classpath files(
			'../cnf/buildrepo/org.eclipse.emf.mwe.core/org.eclipse.emf.mwe.core-1.3.3.jar',
			'../cnf/buildrepo/org.eclipse.emf.mwe.utils/org.eclipse.emf.mwe.utils-1.3.3.jar',
			'../cnf/buildrepo/org.eclipse.emf.mwe2.lib/org.eclipse.emf.mwe2.lib-2.4.1.jar',
			'../cnf/buildrepo/org.eclipse.emf.mwe2.runtime/org.eclipse.emf.mwe2.runtime-2.4.1.jar',
			'../cnf/buildrepo/org.eclipse.emf.ecore/org.eclipse.emf.ecore-2.10.0.jar',
			'../cnf/buildrepo/org.eclipse.emf.ecore.xmi/org.eclipse.emf.ecore.xmi-2.10.0.jar',
			'../cnf/buildrepo/org.eclipse.emf.common/org.eclipse.emf.common-2.10.0.jar',
			'../cnf/buildrepo/org.eclipse.emf.codegen/org.eclipse.emf.codegen-2.10.0.jar',
			'../cnf/buildrepo/org.eclipse.emf.codegen.ecore/org.eclipse.emf.codegen.ecore-2.9.1.jar',
			'../cnf/buildrepo/com.google.guava/com.google.guava-11.0.2.jar'			
		)
	}
}

sourceSets.main.java.srcDir 'main/emf-gen'

// TODO - declare outputs to speed up build
task generateModels << {
	new StandaloneSetup().with {
		scanClassPath = true
		platformUri = ".."
	}

	file(EMF_GEN_DIR).deleteDir()
	
	def genmodelNames = file(MODELSDIR)
		.listFiles()
		.collect{it.name}
		.findAll{it.endsWith(".genmodel")}

	for(genmodel in genmodelNames) {
		new EcoreGenerator().with {
			genModel = "${MODELS}/${genmodel}"
			addSrcPath(SRC_DIR)
			addSrcPath(XTEND_GEN_DIR)
			generateEdit = false
			generateEditor = false
			invoke(null)
		}
	}
	
	file("plugin.xml").delete()
	file("plugin.properties").delete()
	
	println "done. don't forget to refresh the workspace"
}

compileXtend.dependsOn('generateModels')

clean << {
	file(EMF_GEN_DIR).deleteDir()
}
