// build script is based on http://paulonjava.blogspot.de/2013/11/building-bndtools-projects-with-gradle.html

import aQute.bnd.build.Workspace

defaultTasks 'clean', 'bndbuild'

ext.SRC_DIR 		= "main/src"
ext.XTEND_GEN_DIR 	= "main/xtend-gen"

//Add bnd as a build dependency
buildscript {
	repositories { mavenCentral() }
	dependencies {
		classpath files('cnf/plugins/biz.aQute.bnd/biz.aQute.bnd-2.4.0.jar')
		classpath 'org.xtend:xtend-gradle-plugin:0.1.+'
	}
}

//Generate settings.gradle containing all projects
task generateSettings << {
	def f = new File('.')
	def bndProjects = []
	f.eachFile {
		if(new File(it, 'bnd.bnd').exists()) {
			bndProjects << it.name
		}
	}

	def settingsFile = new File('settings.gradle')
	settingsFile.text = 'include '

	bndProjects.eachWithIndex { item,idx ->
		if(idx > 0) {
			settingsFile << ', '
		}

		settingsFile << "'$item'"
	}
}

//Setup the workspace
Workspace workspace
def parentDir = project.projectDir

workspace = Workspace.getWorkspace(parentDir)

subprojects { p ->
	apply plugin: 'xtend'

	compileXtend.doFirst {
		file(XTEND_GEN_DIR).mkdir()
	}
	
	repositories { mavenCentral() }

	aQute.bnd.build.Project bndProject
	bndProject = workspace.getProject(p.projectDir)
	
	//Setup source and target directories
	sourceCompatibility = 1.7
	targetCompatibility = 1.7
	//sourceSets.main.java.srcDirs = ['main/src', 'main/emf-gen', 'main/xtend-gen']
	//sourceSets.main.resources.srcDirs = ['src']
	//sourceSets.test.java.srcDirs = ['test']
	//sourceSets.test.resources.srcDirs = ['test']sourceSets.main.output.classesDir = 'bin'
	//sourceSets.main.output.resourcesDir = 'bin'
	//sourceSets.test.output.classesDir = 'bin_test'
	//sourceSets.test.output.resourcesDir = 'bin_test'
	sourceSets {
		main {
			java.srcDirs = [SRC_DIR, 'main/emf-gen', XTEND_GEN_DIR]
			xtendOutputDir = XTEND_GEN_DIR
		}
	}
	
	
	//testResultsDirName = 'generated/test-reports'

	//Setup project dependencies
	bndProject.getDependson().each {
		compileJava.dependsOn(':' +  it.name + ':bndbuild')
	}

	bndProject.getBuildpath().each {
		dependencies.add('compile', files(it.getFile()))
	}

	//Add junit as a default dependency
	dependencies.add('testCompile', 'junit:junit:4.+')

	//Perform the actual bnd build
	task bndbuild << { task ->
		bndProject.build()

		//Report errors and warnings to the Gradle output
		bndProject.getWarnings().each { logger.warn it }

		bndProject.getErrors().each { logger.error it }

		//Fail the build if there are build errors
		if(bndProject.getErrors()) {
			throw new  GradleException("Bnd build failure. Check build logs for error messages")
		}
	}

	bndbuild.dependsOn([
		'compileJava',
		'processResources'
	])

	//Run integration tests
	task bndtest << { task ->
		if(bndProject.getProperty("Test-Cases")) {
			bndProject.test();
		}
	}

	clean << {
		['generated', XTEND_GEN_DIR].each {
			p.file(it).deleteDir()
		}
	}
}

//Collect generated and external bundles for deployment
task release << {
	delete "release"

	ant.taskdef(name: 'bndrelease', classname: 'aQute.bnd.ant.RunconfigToDistributionTask', classpath: 'cnf/plugins/biz.aQute.bnd/biz.aQute.bnd-2.4.0.jar')
	ant.bndrelease(allowsnapshots: true, bndfile: 'run/webshop.bndrun', rootdir: './', outputdir: 'release')
}

//Generate a Gradle wrapper
task wrapper(type: Wrapper) { gradleVersion = '2.0' }