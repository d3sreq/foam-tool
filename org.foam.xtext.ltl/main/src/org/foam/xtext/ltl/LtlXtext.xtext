grammar org.foam.xtext.ltl.LtlXtext with org.foam.xtext.plogic.PropositionalLogicXtext

import "http://foam.org/models/propositionallogic" // refers to an *.ecore file (here using nsURI)
import "http://foam.org/models/ltl" // refers to an *.ecore file (here using nsURI)
import "http://foam.org/models/plogicXtext"

// =================================================================================
// NOTE: all the "=>" operators were added when fixing issue #23
// (https://github.com/d3sreq/foam-tool/issues/23)
// They prevent warning messages from ANTLR complaining about grammar ambiguity
// see also http://www.eclipse.org/Xtext/documentation.html#antlr_errors
// and also http://zarnekow.blogspot.de/2012/10/xtext-corner-5-backtracking-vs.html 
// =================================================================================

// precedence order: 
// - the unary operators bind stronger than the binary ones.
// - ! and G, F, X, H, O bind equally strong. 
// - U,R,S,T take precedence over &, |, -> and <-> (U binds weaker)
// from: http://www-i2.informatik.rwth-aachen.de/i2/fileadmin/user_upload/documents/MC08/mc_lec13.pdf
// and http://vlsi.colorado.edu/~vis/doc/ctl_ltl/ctl_ltl/ctl_ltl.html

// overwritten to add "starting" rule.
// Without this rule only RuleSimple and RuleUnaryLtl would be used
// as accepting grammar rules
RuleStart returns Formula:
	RuleArrow =>((
		{Until.left=current}		'U' |
		{Releases.left=current}		'R' |
		{Since.left=current}		'S' |
		{Triggered.left=current}	'T'
	) right=RuleArrow)*;

// overwritten rule from propositionallogic to add LTL specific formulas
RuleSimple returns Formula:	
	(
		{Globally}		'G' |
		{Future}		'F' |
		{Next}			'X' |
		{Historically}	'H' |
		{Once}			'O'
	) formula=RuleStart | RuleUnaryPropositionalLogic;
