grammar org.foam.xtext.plogic.PropositionalLogicXtext with org.eclipse.xtext.common.Terminals

import "http://foam.org/models/propositionallogic" // refers to an *.ecore file (here using nsURI)

generate propositionalLogicXtext "http://foam.org/models/plogicXtext"

RuleStart returns Formula:
	RuleArrow;

RuleArrow returns Formula:
	RuleDisjunction (({Implication.left=current} '->' | {Equivalence.left=current} '<->') right=RuleDisjunction)*;

RuleDisjunction returns Formula:
	RuleConjunction ({Or.left=current} '|' right=RuleConjunction)*;

RuleConjunction returns Formula:
	RuleSimple ({And.left=current} '&' right=RuleSimple)*;

// added to be easily overwritten in ltl or ctl grammar 
RuleSimple returns Formula:
	RuleUnaryPropositionalLogic;

RuleUnaryPropositionalLogic returns Formula:
	{Not} '!' formula=RuleStart
	| RuleAtomic;

RuleAtomic returns Formula:
	'(' RuleStart ')'
	| {RuleVariableUse} variable=ID
	| {True} 'true'
	| {False} 'false';
